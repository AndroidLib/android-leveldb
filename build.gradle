buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.0'
    }
}

apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion "19"

    sourceSets {
        main {
            manifest.srcFile 'library/AndroidManifest.xml'
            java.srcDirs = ['library/src']
            resources.srcDirs = ['library/src']
            aidl.srcDirs = ['library/src']
            renderscript.srcDirs = ['library/src']
            res.srcDirs = ['library/res']
            assets.srcDirs = ['library/assets']
            jniLibs.srcDirs = ['library/libs']
        }

        androidTest {
            java.srcDirs = ['tests/src']
            resources.srcDirs = ['tests/src']
            aidl.srcDirs = ['tests/src']
            renderscript.srcDirs = ['tests/src']
            res.srcDirs = ['tests/res']
            assets.srcDirs = ['tests/assets']
        }
    }

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }
}

task ndkBuild(type: Exec) {
    File ndkDir = project.getPlugins().getPlugin('android-library').ndkDirectory
    if (ndkDir == null || !ndkDir.isDirectory()) {
      throw new GradleException("NDK is not configured")
    }
    String ndkBuild = ndkDir.absolutePath + File.separator + 'ndk-build'

    commandLine ndkBuild, '-C', 'library'
}
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.1"
group = "com.litl"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Android LevelDB JNI'
            packaging 'aar'
            description 'Android NDK bindings for LevelDB'
            url 'https://github.com/litl/android-leveldb'

            scm {
                url 'scm:git@github.com:litl/android-leveldb.git'
                connection 'scm:git@github.com:litl/android-leveldb.git'
                developerConnection 'scm:git@github.com:litl/android-leveldb.git'
            }

            licenses {
                license {
                    name 'BSD-style'
                    url 'http://opensource.org/licenses/BSD-3-Clause'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'tingo'
                    name 'Tambet Ingo'
                    email 'tingo@litl.com'
                }
            }
        }
    }
}
